/* HMT project Mandatory Ongoing Maintenance. 
   Summer 2014 version.
*/
buildscript {
    repositories {
        mavenCentral()
	maven {
	  url "http://beta.hpcc.uh.edu/nexus/content/groups/public"
	}
    }
    dependencies {
      classpath group: 'org.homermultitext', name : 'citemgr' , version: '0.4.4'
      classpath group: 'org.homermultitext', name : 'hmt-utils' , version: '0.2.0'

      classpath group: 'edu.harvard.chs', name : 'cite' , version: '0.14.2'
      classpath group: 'edu.holycross.shot', name: 'hocuspocus', version : '0.7.27'

      classpath group: 'net.sf.opencsv', name: 'opencsv', version: '2.3'
    }
}

import org.homermultitext.utils.HmtTokenizer
import org.homermultitext.citemanager.DseManager
import edu.harvard.chs.cite.CiteUrn
import edu.harvard.chs.cite.CtsUrn
import edu.holycross.shot.hocuspocus.Corpus


import au.com.bytecode.opencsv.CSVReader
import groovy.xml.StreamingMarkupBuilder

import org.apache.tools.ant.filters.*

apply plugin: "base"
apply plugin:  "groovy"
apply plugin:  "maven"


apply from: "versions.gradle"


if (hasProperty('conf')) {
    System.err.println "Using configuration data from ${conf}"
    File confFile = new File(conf)
    if (! confFile.exists()) {
        throw new Exception("No configuration file ${conf} found.")
    }
    apply from: conf

} else {
    File confFile = new File("conf.gradle")
    if (! confFile.exists()) {
        throw new Exception("No configuration file ${conf} found.")
    }
    System.err.println "Using conf.gradle for configuration"
    apply from: "conf.gradle"
    
}



if (hasProperty('repoconf')) {
    System.err.println "Using repository locations data from ${repoconf}"
    File confFile = new File(repoconf)
    if (! confFile.exists()) {
        throw new Exception("No file ${repoconf} found.")
    }
    apply from: repoconf

} else {
    File confFile = new File("repos.gradle")
    if (! confFile.exists()) {
        throw new Exception("No configuration file ${confFile} found.")
    }
    System.err.println "Using repos.gradle for finding repositories."
    apply from: "repos.gradle"
}



group = "org.homermultitext"
version = '0.5.0'


repositories {
    mavenCentral()
    maven {
        url "http://beta.hpcc.uh.edu/nexus/content/groups/public"
    }
}

dependencies {
  compile group: 'org.codehaus.groovy', name: 'groovy-all', version: groovyVersion
  compile group: 'org.homermultitext', name : 'citemgr' , version: '0.4.4'

  compile group: 'edu.harvard.chs', name : 'cite' , version: citeversion

  /*
    compile group: 'org.homermultitext', name : 'dse' , version: dseversion


    compile group: "edu.holycross.shot", name: "hocuspocus", version: hocuspocusversion
    compile group: 'edu.harvard.chs', name : 'greekutils' , version: greekutilsversion
  */
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
  /*
    compile group: 'com.thaiopensource', name:'jing', version: '20091111'
    compile group: 'net.sf.saxon', name: 'saxon-dom', version: '8.7'
    compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.5.2'	
  */
}


task palView() {
  description = "Creates HTML page displaying paleographic inventory data."
}

palView.doLast {
  if (! buildDir.exists()) {
    buildDir.mkdir()
  }
  File outputDir = new File(buildDir, "paleography")
  if (! outputDir.exists()) {
    outputDir.mkdir()
  }
  File outputFile = new File(outputDir, "viewer.html")


  String urnBase = "http://beta.hpcc.uh.edu/tomcat/hmt-digital/images?request=GetBinaryImage&w=9000&urn="

  File palFile = new File(paleo)
  CSVReader reader = new CSVReader(new FileReader(palFile))

  StreamingMarkupBuilder xml = new StreamingMarkupBuilder()
  xml.encoding = "UTF-8"

  def doc = xml.bind {
    mkp.xmlDeclaration()
    html {
      head () {
      }
      body{
	table {
	  tr {
	    th ("Reading")
	    th ("Image")
	  }
	  reader.readAll().each { ln ->
	    if (ln.size() >= 3) {
	      tr {
		CtsUrn urn
		String urnVal
		try {
		  urn = new CtsUrn(ln[1])
		  urnVal = urn.getSubref1()
		  td (urnVal)
		  td {
		    img(src : "${urnBase}${ln[2]}")
		  }

		} catch (Exception e) {
		  System.err.println "Bad CTS URN: " + ln[1]
		}
	      }
	    }
	  }
	}
      }
    }
  }
  outputFile.setText(doc.toString(), "UTF-8")
  System.err.println "Paleography observations in ${outputDir}/viewer.html"
} /*end palView task */


task setUpVisInv(type: Copy) {
  from "visualinventory"
  into "${buildDir}/visualinventory"
}


task indexScholia() {
  description = "Reads inventory of scholia, and creates separate indices for surfaces and images"
}


indexScholia.doLast {
  if (!buildDir.exists()) {
    buildDir.mkdir()
  }
  File scholiaToTbs = new File(buildDir, "scholiaToTbs.csv")
  File scholiaToImg = new File(buildDir,"scholiaToImage.csv")

  scholiaInventories.split(/,/).each { inv ->

    if (inv) {
      File invFile = new File(inv)
      if (invFile) {
	CSVReader reader = new CSVReader(new FileReader(invFile))
	reader.readAll().each { ln ->
	  if (ln.size() == 3) {
	    scholiaToTbs.append(ln[0] + ',"' + ln[2] + '"\n')
	    scholiaToImg.append(ln[0] + ',"' + ln[1] + '"\n')
	  }
	}
      }
    }
  }
}


task dse(dependsOn : [setUpVisInv, indexScholia]) {
  description = "Does citemgr's DSE validation,  and creates an XML visual inventory for the specified folio."
}

dse.doLast {
  DseManager dse = new DseManager()

  folioToImage.split(/,/).each { idx ->
    if (dse.tbsImageIndexFiles) {
      dse.tbsImageIndexFiles.add(new File(idx))
    } else {
      dse.tbsImageIndexFiles = [new File(idx)]
    }
  }

  textToImage.split(/,/).each { idx ->
    if (dse.textImageIndexFiles) {
      dse.textImageIndexFiles.add(new File(idx))
    } else {
      dse.textImageIndexFiles = [new File(idx)]
    }
  }
  dse.textImageIndexFiles.add(new File(buildDir, "scholiaToImage.csv"))

  //System.err.printn ""

  textToFolio.split(/,/).each { idx ->
    if (dse.textTbsIndexFiles) {
      dse.textTbsIndexFiles.add(new File(idx))
    } else {
      dse.textTbsIndexFiles = [new File(idx)]
    }
  }
  dse.textTbsIndexFiles.add(new File (buildDir, "scholiaToTbs.csv"))

  println "textTbs idx: " + dse.textTbsIndexFiles


  CiteUrn  img = dse.imageForTbs(folio)

  System.err.println "Default image: " + img

  if (!img) {
    System.err.println "No default image for " + folio
  }
  def iMaps 
  try {
    iMaps = dse.imageMapsByText(img)
  } catch (Exception e)  {
    System.err.println "Unable to get image maps for ${img}."
  }

  CiteUrn urn = new CiteUrn(folio)
  String xml =  dse.getVisualInventoryXml(urn)

  String fName = "${buildDir}/visualinventory/inventory.xml"
  File visinv = new File(fName)
  visinv.setText(xml,"UTF-8")

  dse.verifyTbs(folio)

}


task tabulate() {
  description = "Writes tabulated form of all inventoried texts in ${buildDir}/tabulated."
}


tabulate.doLast {
  File tiFile = new File(textInventory)
  File archiveDir = new File(textArchive)
  File outDir = new File(buildDir,"tabulated")
  if (! buildDir.exists()) { 
    buildDir.mkdir()
  }
  if (! outDir.exists()) {
    outDir.mkdir()
  }

  Corpus c = new Corpus(tiFile, archiveDir)
  c.tabulateRepository(outDir)
}

task tokenize(dependsOn: tabulate) {
  description = "Creates HMT classified tokenization of texts for given page"
  
}

tokenize.doLast {
  File tabsDir = new File(buildDir, "tabulated")
  File outputDir = new File(buildDir, "tokens")
  if (! outputDir.exists()) {
    outputDir.mkdir()
  }
  File outputFile = new File(outputDir, "tokens.txt")
  String fldSeparator = "#"
  HmtTokenizer hmtTokens = new HmtTokenizer(tabsDir, outputFile, fldSeparator)
  hmtTokens.tokenizeTabs()
}


task namedents(dependsOn: [tokenize]) {
  description  = "Check that named entities are valid."
}
namedents.doLast {
  File tokens = new File("${buildDir}/tokens/tokens.txt")
  def nes = tokens.readLines().findAll { l -> l ==~ /urn:cite:hmt.+/}
  println "named ents: " + nes.size() + " (class ${nes.getClass()})"
}

task verify(dependsOn: [dse, namedents]) {
    description = "Runs full verification suite on requested URN(s)"
}

verify.doFirst {
  System.out.println "Verifying folio ${folio}\n"
}


verify.doLast {
  
  DseManager dse = new DseManager()
  dse.debug = 5

  

  System.out.println "Completed verification of ${folio}."
  System.out.println "Reports are in ${buildDir}/verification."
  System.out.println "\nMOM version: ${version}."
}